prop asteroid.Health

pub fn asteroid.Asteroid(
    radius=4, speed=30*Random, dissipate=0.75s, color=#444444,
    pos = @(Random * BoundaryRadius * PickRandom([1,-1]), -BoundaryRadius - radius),
    generation=0, parent?, health=10
) {
    use body=asteroid

    Health=health
    
    Body(
        pos=,
        velocity = @(speed * Random * PickRandom([0.2,-0.2]), speed),
    )
    ImageSprite(@asteroid.png, shading=0.25, shadow=0.5)
    PolygonCollider(shape=Circle, category=Category:Asteroid)
    RecoverSpeed

    DespawnOutsideBoundary

    // on Paint {
    //     Spark(color=, fade=color.Darken(0.5), splatter=0.5, layer=-10, shadow=0.5)
    // }

    on BeforeCollide that {
        Strobe(shine=0.5, dissipate=0.5s)
        repeat 5 { Spark(color=#222222, shine=0, splatter=1, dissipate=0.5s, radius=Random, velocity=Velocity*RandomVector) }
        const MaxGenerations = 2
        if that.Category.Overlaps(Category:Asteroid) {
            Health -= 8
        } else {
            Health -= 3
        }
        if Health <= 0 {
            Expire
            if generation < MaxGenerations {            
                let parent = asteroid
                let gen = generation + 1
                repeat 2 {
                    Spawn asteroid {
                        Asteroid(
                            pos=Pos+RandomVector, parent=, generation=gen,
                            radius=0.6*radius,
                            speed=1.3*speed,
                            dissipate=0.7*dissipate,
                            color=Mix(0.25, color, #ffff00),
                            health=8/gen
                        )
                    }
                }
            }
        }
    }

}