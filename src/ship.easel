pub collect ship.BlastCount = 1
pub prop ship.Powerup = 'None'
pub field ship.ShieldOn = false

signal ship.ReadyPlayer
field ship.TurboOn = false

symbol Forward
symbol Rightward
symbol Leftward
symbol Backward

pub fn ship.Ship([owner]) {
    use body=ship, radius=1
    
    Body(pos = @(0, BoundaryRadius - 16))
    PolygonCollider(shape=Circle, category=Category:Ship, density=2)
    
    ImageSprite<initialShield>(@assets/ship.svg, overlay=15, ownerColor=1, flicker=1, opacity=0.7, scale=4, layer=6)
    ImageSprite(@assets/ship.svg, shading=0.3, scale=4, layer=5)
    
    TextSprite(owner.PlayerName, anchorTop=true, bodyOffset=@(0, 4), heightPx=22, noRotation=true, layer=-15, ownerColor=true, opacity=0.5)
    PointerAimingLine(body=ship, color=#ff0000, opacity=0.4, radius=0.1, layer=-20, glare=0.5)
    
    LimitSpeed(maxSpeed=SHIP_MAX_SPEED)
    DecayTurnRate(SHIP_MAX_SPEED)
    CollideBoundary
    WrapOutsideBoundary

    once Tick {
        Heading = -RIGHT_ANGLE 
    }

    once Tick(3s) { 
        ReadyPlayer 
    }

    with Pointer {
        Heading = Angle(Pointer - Pos)
    }

    on ButtonDown(KeyShiftLeft) {
        TurboOn = true
    }

    on ButtonUp(KeyShiftLeft) {
        TurboOn = false
    }
    
    on ButtonDown(ArrowUp), ButtonDown(KeyW) {
        Propel<Forward>(Direction(Heading))
    }

    on ButtonDown(ArrowLeft), ButtonDown(KeyA) {
        Propel<Leftward>(@(-1, 0))
    }

    on ButtonDown(ArrowRight), ButtonDown(KeyD) {
        Propel<Rightward>(@(1, 0))
    }

    on ButtonDown(ArrowDown), ButtonDown(KeyS) {
        Propel<Backward>(@(0, 1))
    }

    on ButtonUp(ArrowUp), ButtonUp(KeyW) {
        Halt<Forward>
    }
    
    on ButtonUp(ArrowLeft), ButtonUp(KeyA) {
        Halt<Leftward>
    }
    
    on ButtonUp(ArrowRight), ButtonUp(KeyD) {
        Halt<Rightward>
    }
    
    on ButtonUp(ArrowDown), ButtonUp(KeyS) {
        Halt<Backward>
    }

    on ButtonDown(Click) {
        behavior<laser> {
            loop {
                if(BlastCount > 1) {
                    Hear(@assets/basic_laser3.mp3)
                } else {
                    surprise {
                        Hear(@assets/basic_laser.mp3),
                        Hear(@assets/basic_laser2.mp3),
                    }
                }
                
                const anglePerBolt = 0.03rev
                let initialAngle = -(anglePerBolt * (BlastCount - 1)) / 2
                for i in Range(0, BlastCount) {
                    Spawn projectile {
                        Projectile(ship=, headingOffset = initialAngle + i*anglePerBolt)
                    }
                }
                
                await Tick(0.3s)
            }
        }
    }

    on ButtonUp(Click) {
        delete behavior<laser>
    }

    await ReadyPlayer
    
    delete ImageSprite<initialShield>
    
    on BeforeCollide that {
        if ShieldOn {
            return
        }
        
        if that.Category.Overlaps(Category:Asteroid | Category:EnemyShip | Category:Explosion) {
            Hear(@assets/explosion1.mp3)
            
            repeat 5 {
                Spark(speed=10, dissipate=0.75s, feather=1, splatter=1, color=#ffffff, bloom=3, luminous=1)
            }
            
            Transmission(audience=Audience:All) {
                P {
                    PlayerNameDisplay
                    " was destroyed."
                }
            }
            
            Expire
            break
        }
    }
}

fn ship.Propel<Path> (direction, [body, owner]) {
    behavior<Path+brake> on BeforePhysics {
        if Velocity.X > 0 {
            Velocity = @(Velocity.X * SHIP_BRAKE, Velocity.Y)
        }
    }
    
    // NOTE: to lock on to target (Pointer)
    // ApplyImpulse(impulse * RotateLeft(Pointer - Pos))
    // ApplyTurningImpulse(impulse)
    behavior<Path+thrust> on BeforePhysics {
        delete behavior<Path+brake>
        let impulse = TurboOn ? SHIP_TURBO : SHIP_PROPEL
        ApplyImpulse(impulse * direction)
        Heading = Angle(Pointer - Pos)
    }

    behavior<Path+exhaust> on Paint {
        if TurboOn {
            Spark(radius=1.2, velocity=-25 * direction, 
                  bodyOffset=@(-2,0), layer=-5, 
                  dissipate=0.2s, splatter=0.2,
                  color=#0011ff, bloom=1, luminous=1, shine=0.8)
        } else {
            Spark(radius=0.7, velocity=-20 * direction, 
                  bodyOffset=@(direction.Y, direction.X), 
                  layer=-5, dissipate=0.2s, splatter=0.2, fade=1,
                  color=#ff8800, bloom=1, luminous=1, shine=0.5)
        }
    }
}

fn ship.Halt<Path> {
    delete behavior<Path+thrust>
    delete behavior<Path+exhaust>
}
