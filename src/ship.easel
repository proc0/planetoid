pub collect ship.NumPlasmaBolts = 1
signal ship.ReadyToPlay
pub field ship.HasTurbo = false

pub const SHIP_ACCEL = 3
pub const SHIP_TURBO = 9
pub const RIGHT_ANGLE = Pi/2

pub fn ship.Ship([owner]) {
    use body=ship
    use radius=1

    Body(pos = BoundaryRadius * Random * RandomVector)
    PolygonCollider(shape=Circle, category=Category:Ship, density=2)
    
    PolygonSprite<initialShield>(shape=Circle(radius=3), layer=-5, ownerColor=0.45, color=#00ffaa, luminous=1, bloom=3, shading=0.1, shadow=0.5)
    ImageSprite(@assets/ship_body.png, ownerColor=0.45, shading=0.25, shadow=0.5, scale=3)
    
    TextSprite(owner.PlayerName, noRotation=true, screenOffset=@(0, 3.5), layer=-15, ownerColor=true, opacity=0.5, strobe=false, heightPx=22, anchorTop=true, luminous=1)
    PointerAimingLine(body=ship, color=#888888, opacity=0.1, radius=0.5, layer=-20)
    
    LimitSpeed(maxSpeed=30)
    DecayTurnRate
    CollideBoundary
    WrapOutsideBoundary
    
    on ButtonDown(ArrowUp), ButtonDown(KeyW) {
        behavior<thrust> on BeforePhysics {
            let impulse = HasTurbo ? SHIP_TURBO : SHIP_ACCEL
            ApplyImpulse(impulse * Direction(Pointer - Pos))
        }

        behavior<thrustSparks> on Paint {
            let color = HasTurbo ? #0011ff : #ff8800
            let cardinal = @(0, -1)
            let radius = HasTurbo ? 1.2 : 0.1
            Spark(radius=, color=, bodyOffset=@(-2,0), layer=-5, velocity=-25*cardinal, dissipate=0.3s, luminous=1, shine=0.5, bloom=1, splatter=0.4)
        }
    }

    on ButtonDown(KeyShiftLeft) {
        HasTurbo = true
    }

    on ButtonUp(KeyShiftLeft) {
        HasTurbo = false
    }

    on ButtonUp(ArrowUp), ButtonUp(KeyW) {
        delete behavior<thrust>
        delete behavior<thrustSparks>
    }

    on ButtonDown(ArrowLeft), ButtonDown(KeyA) {
        behavior<strafeLeft> on BeforePhysics {
            let impulse = HasTurbo ? SHIP_TURBO : SHIP_ACCEL
            let cardinal = @(-1, 0)
            ApplyImpulse(impulse * cardinal)
        }

        behavior<strafeLeftSparks> on Paint {
            if HasTurbo {
                Spark(
                    color=#0011ff, bodyOffset=@(0, -1), dissipate=0.5s, radius=1.2,
                    luminous=1, shine=0.5, bloom=1, layer=-5,
                    splatter=0.4, velocity=-25*@(-1, 0)
                )
            } else {
                Spark(
                    color=#ff8800, bodyOffset=@(0, -1), dissipate=0.5s, radius=0.1*radius,
                    luminous=1, shine=0.5, bloom=1, layer=-5,
                    splatter=1, velocity=-25*@(-1, 0)
                )
            }
        }
    }

    on ButtonUp(ArrowLeft), ButtonUp(KeyA) {
        delete behavior<strafeLeft>
        delete behavior<strafeLeftSparks>
    }

    on ButtonDown(ArrowRight), ButtonDown(KeyD) {
        behavior<strafeRight> on BeforePhysics {
            let impulse = HasTurbo ? SHIP_TURBO : SHIP_ACCEL
            let cardinal = @(1, 0)
            ApplyImpulse(impulse * cardinal)
        }

        behavior<strafeRightSparks> on Paint {
            if HasTurbo {
                Spark(
                    color=#0011ff, bodyOffset=@(0, 1), dissipate=0.5s, radius=1.2,
                    luminous=1, shine=0.5, bloom=1, layer=-5,
                    splatter=0.4, velocity=-25*@(1, 0)
                )
            } else {
                Spark(
                    color=#ff8800, dissipate=0.5s, radius=0.1*radius,
                    luminous=1, shine=0.5, bloom=1, layer=-5,
                    splatter=1, velocity=-25*@(1, 0)
                ) 
            }
        }
    }

    on ButtonUp(ArrowRight), ButtonUp(KeyD) {
        delete behavior<strafeRight>
        delete behavior<strafeRightSparks>
    }

    on ButtonDown(ArrowDown), ButtonDown(KeyS) {
        behavior<thrustReverse> on BeforePhysics {
            let impulse = HasTurbo ? SHIP_TURBO : SHIP_ACCEL
            let cardinal = @(0, 1)
            ApplyImpulse(impulse * cardinal)
        }

        behavior<thrustReverseSparks> on Paint {
            if HasTurbo {
                Spark(
                    color=#0011ff, bodyOffset=@(0,1), dissipate=0.5s, radius=1.2,
                    luminous=1, shine=0.5, bloom=1, layer=-5,
                    splatter=0.4, velocity=-25*@(0, 1)
                )
            } else {
                Spark(
                    color=#ff8800, dissipate=0.5s, radius=0.1*radius,
                    luminous=1, shine=0.5, bloom=1, layer=-5,
                    splatter=1, velocity=-25*@(0, 1)
                )
            }
        }
    }

    on ButtonUp(ArrowDown), ButtonUp(KeyS) {
        delete behavior<thrustReverse>
        delete behavior<thrustReverseSparks>
    }

    // on ButtonDown(ArrowDown), ButtonDown(KeyS) {
    //     delete behavior<thrust>
    //     delete behavior<thrustSparks>
    //     delete behavior<strafeLeft>
    //     delete behavior<strafeLeftSparks>
    //     delete behavior<strafeRight>
    //     delete behavior<strafeRightSparks>
    //     delete behavior<thrustReverse>
    //     delete behavior<thrustReverseSparks>
    //     Velocity = Velocity*0.3
    // }

    on ButtonDown(Click) {
        behavior<shootBullet> {
            loop {
                const anglePerBolt = 0.03rev
                let initialAngle = -(anglePerBolt * (NumPlasmaBolts - 1)) / 2
                for i in Range(0, NumPlasmaBolts) {                
                    Spawn projectile {
                        PlasmaBolt(ship=, headingOffset = initialAngle + i*anglePerBolt)
                    }
                }
                await Tick(0.3s)
            }
        }
    }

    on ButtonUp(Click) {
        delete behavior<shootBullet>
    }

    with Pointer {
        Heading = Angle(Pointer - Pos)
    }

    once Tick(3s) {
        ReadyToPlay
    }
    
    await ReadyToPlay
    
    delete PolygonSprite<initialShield>
    
    on BeforeCollide that {
        if QueryCount(filter=Category:Shield) > 0 {
            return
        }
        
        if that.Category.Overlaps(Category:Asteroid | Category:Alien | Category:Explosion) {
            repeat 5 {
                Spark(color=#ffffff, luminous=1, bloom=3, feather=1, dissipate=0.75s, splatter=1, speed=10)
            }

            Transmission(audience=Audience:All) {
                P {
                    PlayerNameDisplay
                    "'s ship was destroyed."
                }
            }
            
            Expire
            break
        }
    }
}