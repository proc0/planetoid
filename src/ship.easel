pub collect ship.BlastCount = 1
pub field ship.ShieldOn = false
pub prop ship.Health

signal ship.ReadyPlayer
field ship.TurboOn = false

symbol Forward
symbol Rightward
symbol Leftward
symbol Backward

pub fn ship.Ship([owner]) {
    use body = ship
    use radius = 1
    use layer = 5
    Health = SHIP_HEALTH
    
    Body(pos=@(0, BOUNDS_RADIUS - 16))
    PolygonCollider(shape=Circle, category=Category:Ship, density=2)
    
    ImageSprite<invulnerable>(@assets/ship.svg, scale=4, overlay=1, flicker=1, ownerColor=1, opacity=0.7)
    ImageSprite(@assets/ship.svg, scale=4, shading=0.3)
    
    PointerAimingLine(body=ship, layer=4, radius=0.1, color=#ff0000, opacity=0.5, glare=0.5)
    TextSprite(owner.PlayerName, layer=4, heightPx=22, bodyOffset=@(0, 4), anchorTop=true, noRotation=true, ownerColor=true, opacity=0.5)
    
    LimitSpeed(maxSpeed=SHIP_MAX_SPEED)
    DecayTurnRate(SHIP_DECAY_TURN)
    TeleportBoundsX
    CollideBounds

    DisplayHealth
    
    once Tick {
        Heading = NEG_HALF_PI 
    }

    once Tick(3s) { 
        ReadyPlayer 
    }

    with Pointer {
        Heading = Angle(Pointer - Pos)
    }

    on ButtonDown(KeyShiftLeft) {
        TurboOn = true
    }

    on ButtonUp(KeyShiftLeft) {
        TurboOn = false
    }
    
    on ButtonDown(ArrowUp), ButtonDown(KeyW) {
        Propel<Forward>(Direction(Heading))
    }

    on ButtonDown(ArrowLeft), ButtonDown(KeyA) {
        Propel<Leftward>(LEFT)
    }

    on ButtonDown(ArrowRight), ButtonDown(KeyD) {
        Propel<Rightward>(RIGHT)
    }

    on ButtonDown(ArrowDown), ButtonDown(KeyS) {
        Propel<Backward>(DOWN)
    }

    on ButtonUp(ArrowUp), ButtonUp(KeyW) {
        Halt<Forward>
    }
    
    on ButtonUp(ArrowLeft), ButtonUp(KeyA) {
        Halt<Leftward>
    }
    
    on ButtonUp(ArrowRight), ButtonUp(KeyD) {
        Halt<Rightward>
    }
    
    on ButtonUp(ArrowDown), ButtonUp(KeyS) {
        Halt<Backward>
    }

    on ButtonDown(KeySpacebar), ButtonDown(Click) {
        behavior<laser> {
            loop {
                const angle = 0.03rev
                let initial = -(angle * (BlastCount - 1)) / 2
                for index in Range(0, BlastCount) {
                    Spawn weapon {
                        Laser(source=ship, offset=initial + index * angle)
                    }
                }

                if(BlastCount > 1) {
                    Hear(@assets/basic_laser3.mp3)
                } else {
                    surprise {
                        Hear(@assets/basic_laser.mp3),
                        Hear(@assets/basic_laser2.mp3),
                    }
                }
                // TODO: make this a powerup
                await Tick(SHIP_FIRE_RATE)
            }
        }
    }

    on ButtonUp(Click) {
        delete behavior<laser>
    }

    await ReadyPlayer
    delete ImageSprite<invulnerable>
    
    on BeforeCollide that {
        if ShieldOn { return }

        if that.Category.Overlaps(Category:Asteroid | Category:Enemy | Category:Explosion | Category:Projectile) {
            Health -= 3
            
            DisplayHealth
            
            if Health <= 0 {
                Hear(@assets/explosion1.mp3)
    
                // TODO: abstract explosions
                repeat 5 {
                    Spark(speed=10, dissipate=0.75s, feather=1, splatter=1, color=#ffffff, bloom=3, luminous=1)
                }
                
                Transmission(audience=Audience:All) {
                    P { 
                        PlayerNameDisplay
                        " was destroyed."
                    }
                }
                
                Expire
                break
            }
            
            let opacity = 0.7
            behavior<hit> on Paint {            
                ImageSprite(@assets/ship.svg, color=#ff0000, scale=4, opacity=, luminous=0.3)
                Vignette(color=#ff0000, opacity=opacity-0.3, power=40)

                opacity -= 0.03
            }
            
            once Tick(0.25s) {
                delete behavior<hit>
            }
        }
    }
}

fn ship.Propel<Path> (direction, [body, owner]) {
    behavior<Path+brake> on BeforePhysics {
        if Velocity.X > 0 {
            Velocity = @(Velocity.X * SHIP_BRAKE, Velocity.Y)
        }
    }
    
    // NOTE: to lock on to target (Pointer)
    // ApplyImpulse(impulse * RotateLeft(Pointer - Pos))
    // ApplyTurningImpulse(impulse)
    behavior<Path+thrust> on BeforePhysics {
        delete behavior<Path+brake>
        let impulse = TurboOn ? SHIP_TURBO : SHIP_PROPEL
        ApplyImpulse(impulse * direction)
        Heading = Angle(Pointer - Pos)
    }

    behavior<Path+exhaust> on Paint {
        if TurboOn {
            Spark(radius=1, velocity=-25 * direction, 
                  bodyOffset=@(-2, 0), layer=4, 
                  dissipate=0.2s, splatter=0.6,
                  color=#0011ff, bloom=1, luminous=1, shine=0.8)
        } else {
            Spark(radius=0.7, velocity=-20 * direction, 
                  bodyOffset=@(direction.Y*2, -direction.X), layer=4,
                  dissipate=0.3s, splatter=0.4, fade=1, feather=1,
                  color=#ff8800, bloom=1, luminous=1, shine=0.5)
        }
    }
}

fn ship.Halt<Path> {
    delete behavior<Path+thrust>
    delete behavior<Path+exhaust>
}
