signal ship.ReadyPlayer

pub collect ship.BlastCount = 1
pub prop ship.Powerup = 'None'

field ship.Boosting = false
// field ship.Pointing = @(0, 0)

pub fn ship.Ship([owner]) {
    use body=ship
    use radius=1
    
    Body(pos = @(0, BoundaryRadius - 16))
    PolygonCollider(shape=Circle, category=Category:Ship, density=2)
    
    ImageSprite<initialShield>(@assets/ship.svg, overlay=15, ownerColor=1, flicker=1, opacity=0.7, scale=4, layer=6)
    ImageSprite(@assets/ship.svg, shading=0.3, scale=4, layer=5)
    
    TextSprite(owner.PlayerName, anchorTop=true, bodyOffset=@(0, 4), heightPx=22, noRotation=true, layer=-15, ownerColor=true, opacity=0.5)
    PointerAimingLine(body=ship, color=#ff0000, opacity=0.4, radius=0.1, layer=-20, glare=0.5)
    
    LimitSpeed(maxSpeed=30)
    DecayTurnRate(0.1)
    CollideBoundary
    WrapOutsideBoundary

    once Tick {
        Heading = -RIGHT_ANGLE 
    }

    once Tick(3s) { 
        ReadyPlayer 
    }

    with Pointer {
        Heading = Angle(Pointer - Pos)
    }

    on ButtonDown(KeyShiftLeft) {
        Boosting = true
    }

    on ButtonUp(KeyShiftLeft) {
        Boosting = false
    }
    
    on ButtonDown(ArrowUp), ButtonDown(KeyW) {
        let pointing = @(0, -1)
         
        behavior<thrust> on BeforePhysics {
            let impulse = Boosting ? SHIP_BOOST : SHIP_THRUST
            ApplyImpulse(impulse * Direction(Heading))
        }

        behavior<exhaust> on Paint {
            let bodyOffset = @(-2,0)
            let color = Boosting ? #0011ff : #ff8800
            let radius = Boosting ? 1.2 : 0.1
            let velocity = -25 * pointing
            Spark(radius=, velocity=, 
                  bodyOffset=, layer=-5, 
                  dissipate=0.3s, splatter=0.4, 
                  color=, bloom=1, luminous=1, shine=0.5)
        }
    }

    on ButtonDown(ArrowLeft), ButtonDown(KeyA) {
        let pointing = @(-1, 0)

        behavior<stopRight> on BeforePhysics {
            if Velocity.X > 0 {
                Velocity = @(Velocity.X*SHIP_BREAK, Velocity.Y)
            }
        }
        // use this to lock into target:
        // ApplyImpulse(impulse * RotateLeft(Pointer - Pos))
        // ApplyTurningImpulse(impulse)
        behavior<strafeLeft> on BeforePhysics {
            delete behavior<stopRight>
            let impulse = Boosting ? SHIP_BOOST : SHIP_THRUST
            ApplyImpulse(impulse * pointing)
            // ApplyImpulse(impulse * RotateLeft(Pointer - Pos))
            // ApplyTurningImpulse(impulse)
            Heading = Angle(Pointer - Pos)
        }

        behavior<exhaustRight> on Paint {
            let bodyOffset = Boosting ? @(-2,0) : @(0, -1)
            let color = Boosting ? #0011ff : #ff8800
            let radius = Boosting ? 1.2 : 0.1
            let velocity = -25 * pointing
            Spark(radius=, velocity=, 
                  bodyOffset=, layer=-5, 
                  dissipate=0.3s, splatter=0.4, 
                  color=, bloom=1, luminous=1, shine=0.5)
        }
    }

    on ButtonDown(ArrowRight), ButtonDown(KeyD) {
        let pointing = @(1, 0)

        behavior<stopLeft> on BeforePhysics {
            if Velocity.X < 0 {
                Velocity = @(Velocity.X*SHIP_BREAK, Velocity.Y)
            }
        }
        
        behavior<strafeRight> on BeforePhysics {
            delete behavior<stopLeft>
            let impulse = Boosting ? SHIP_BOOST : SHIP_THRUST
            ApplyImpulse(impulse * pointing)
            // ApplyImpulse(impulse * RotateRight(Pointer - Pos))
            // ApplyTurningImpulse(impulse)
            Heading = Angle(Pointer - Pos)
        }

        behavior<exhaustLeft> on Paint {
            let bodyOffset = Boosting ? @(-2,0) : @(0, 1)
            let color = Boosting ? #0011ff : #ff8800
            let radius = Boosting ? 1.2 : 0.1
            let velocity = -25 * pointing
            Spark(radius=, velocity=, 
                  bodyOffset=, layer=-5, 
                  dissipate=0.3s, splatter=0.4, 
                  color=, bloom=1, luminous=1, shine=0.5)
        }
    }

    on ButtonDown(ArrowDown), ButtonDown(KeyS) {
        let pointing = @(0, 1)

        behavior<stopReverse> on BeforePhysics {
            if Velocity.X < 0 {
                Velocity = @(Velocity.X*SHIP_BREAK, Velocity.Y)
            }
        }
        
        behavior<reverse> on BeforePhysics {
            delete behavior<stopReverse>
            let impulse = Boosting ? SHIP_BOOST : SHIP_THRUST
            ApplyImpulse(impulse * pointing)
            Heading = Angle(Pointer - Pos)
        }

        behavior<exhaustReverse> on Paint {
            let bodyOffset = Boosting ? @(-2,0) : @(1, 0)
            let color = Boosting ? #0011ff : #ff8800
            let radius = Boosting ? 1.2 : 0.1
            let velocity = -25 * pointing
            Spark(radius=, velocity=, 
                  bodyOffset=, layer=-5, 
                  dissipate=0.3s, splatter=0.4, 
                  color=, bloom=1, luminous=1, shine=0.5)
        }
    }

    on ButtonUp(ArrowUp), ButtonUp(KeyW) {
        delete behavior<thrust>
        delete behavior<exhaust>
    }
    
    on ButtonUp(ArrowLeft), ButtonUp(KeyA) {
        delete behavior<strafeLeft>
        delete behavior<exhaustRight>
    }
    
    on ButtonUp(ArrowRight), ButtonUp(KeyD) {
        delete behavior<strafeRight>
        delete behavior<exhaustLeft>
    }
    
    on ButtonUp(ArrowDown), ButtonUp(KeyS) {
        delete behavior<reverse>
        delete behavior<exhaustReverse>
    }

    on ButtonDown(Click) {
        behavior<laser> {
            loop {
                if(BlastCount > 1) {
                    Hear(@assets/basic_laser3.mp3)
                } else {
                    surprise {
                        Hear(@assets/basic_laser.mp3),
                        Hear(@assets/basic_laser2.mp3),
                    }
                }
                
                const anglePerBolt = 0.03rev
                let initialAngle = -(anglePerBolt * (BlastCount - 1)) / 2
                for i in Range(0, BlastCount) {
                    Spawn projectile {
                        Projectile(ship=, headingOffset = initialAngle + i*anglePerBolt)
                    }
                }
                
                await Tick(0.3s)
            }
        }
    }

    on ButtonUp(Click) {
        delete behavior<laser>
    }

    await ReadyPlayer
    
    delete ImageSprite<initialShield>
    
    on BeforeCollide that {
        if QueryCount(filter=Category:Shield) > 0 {
            return
        }
        
        if that.Category.Overlaps(Category:Asteroid | Category:EnemyShip | Category:Explosion) {
            Hear(@assets/explosion1.mp3)
            
            repeat 5 {
                Spark(speed=10, dissipate=0.75s, feather=1, splatter=1, color=#ffffff, bloom=3, luminous=1)
            }
            
            Transmission(audience=Audience:All) {
                P {
                    PlayerNameDisplay
                    " was destroyed."
                }
            }
            
            Expire
            break
        }
    }
}
