signal ship.ReadyPlayer

pub collect ship.BlastCount = 1
pub prop ship.Powerup = 'None'

field ship.HasTurbo = false

pub fn ship.Ship([owner]) {
    use body=ship
    use radius=1
    
    Body(pos = @(0, BoundaryRadius - 16))
    PolygonCollider(shape=Circle, category=Category:Ship, density=2)
    
    ImageSprite<initialShield>(@assets/ship_body.png, overlay=15, ownerColor=1, flicker=1, shine=0.3, shading=0.25, shadow=0, scale=4, layer=6)
    ImageSprite(@assets/ship_body.png, shading=0.25, shine=0, shadow=0, scale=4, layer=5)
    
    TextSprite(owner.PlayerName, noRotation=true, screenOffset=@(0, 4), layer=-15, ownerColor=true, opacity=0.5, strobe=false, heightPx=22, anchorTop=true)
    PointerAimingLine(body=ship, color=#ff0000, opacity=0.4, radius=0.1, layer=-20)
    
    LimitSpeed(maxSpeed=30)
    DecayTurnRate(0.5)
    CollideBoundary
    WrapOutsideBoundary

    once Tick {
       Heading = -RIGHT_ANGLE 
    }

    once Tick(3s) { 
        ReadyPlayer 
    }

    with Pointer {
        Heading = Angle(Pointer - Pos)
    }

    on ButtonDown(KeyShiftLeft) {
        HasTurbo = true
    }

    on ButtonUp(KeyShiftLeft) {
        HasTurbo = false
    }
    
    on ButtonDown(ArrowUp), ButtonDown(KeyW) {
        behavior<thrust> on BeforePhysics {
            let impulse = HasTurbo ? SHIP_TURBO : SHIP_THRUST
            ApplyImpulse(impulse * Direction(Pointer - Pos))
        }

        behavior<thrustSparks> on Paint {
            let cardinal = @(0, -1)
            let radius = HasTurbo ? 1.2 : 0.1
            let color = HasTurbo ? #0011ff : #ff8800
            Spark(radius=, color=, bodyOffset=@(-2,0), layer=-5, velocity=-25*cardinal, dissipate=0.3s, luminous=1, shine=0.5, bloom=1, splatter=0.4)
        }
    }

    on ButtonUp(ArrowUp), ButtonUp(KeyW) {
        delete behavior<thrust>
        delete behavior<thrustSparks>
    }

    on ButtonDown(ArrowLeft), ButtonDown(KeyA) {
        behavior<strafeLeft> on BeforePhysics {
            let impulse = HasTurbo ? SHIP_TURBO : SHIP_THRUST
            let cardinal = @(-1, 0)
            ApplyImpulse(impulse * cardinal)
        }

        behavior<strafeLeftSparks> on Paint {
            if HasTurbo {
                Spark(
                    color=#0011ff, bodyOffset=@(0, -1), dissipate=0.5s, radius=1.2,
                    luminous=1, shine=0.5, bloom=1, layer=-5,
                    splatter=0.4, velocity=-25*@(-1, 0)
                )
            } else {
                Spark(
                    color=#ff8800, bodyOffset=@(0, -1), dissipate=0.5s, radius=0.1*radius,
                    luminous=1, shine=0.5, bloom=1, layer=-5,
                    splatter=1, velocity=-25*@(-1, 0)
                )
            }
        }
    }

    on ButtonUp(ArrowLeft), ButtonUp(KeyA) {
        delete behavior<strafeLeft>
        delete behavior<strafeLeftSparks>
    }

    on ButtonDown(ArrowRight), ButtonDown(KeyD) {
        behavior<strafeRight> on BeforePhysics {
            let impulse = HasTurbo ? SHIP_TURBO : SHIP_THRUST
            let cardinal = @(1, 0)
            ApplyImpulse(impulse * cardinal)
        }

        behavior<strafeRightSparks> on Paint {
            if HasTurbo {
                Spark(
                    color=#0011ff, bodyOffset=@(0, 1), dissipate=0.5s, radius=1.2,
                    luminous=1, shine=0.5, bloom=1, layer=-5,
                    splatter=0.4, velocity=-25*@(1, 0)
                )
            } else {
                Spark(
                    color=#ff8800, dissipate=0.5s, radius=0.1*radius,
                    luminous=1, shine=0.5, bloom=1, layer=-5,
                    splatter=1, velocity=-25*@(1, 0)
                ) 
            }
        }
    }

    on ButtonUp(ArrowRight), ButtonUp(KeyD) {
        delete behavior<strafeRight>
        delete behavior<strafeRightSparks>
    }

    on ButtonDown(ArrowDown), ButtonDown(KeyS) {
        behavior<thrustReverse> on BeforePhysics {
            let impulse = HasTurbo ? SHIP_TURBO : SHIP_THRUST
            let cardinal = @(0, 1)
            ApplyImpulse(impulse * cardinal)
        }

        behavior<thrustReverseSparks> on Paint {
            if HasTurbo {
                Spark(
                    color=#0011ff, bodyOffset=@(0,1), dissipate=0.5s, radius=1.2,
                    luminous=1, shine=0.5, bloom=1, layer=-5,
                    splatter=0.4, velocity=-25*@(0, 1)
                )
            } else {
                Spark(
                    color=#ff8800, dissipate=0.5s, radius=0.1*radius,
                    luminous=1, shine=0.5, bloom=1, layer=-5,
                    splatter=1, velocity=-25*@(0, 1)
                )
            }
        }
    }

    on ButtonUp(ArrowDown), ButtonUp(KeyS) {
        delete behavior<thrustReverse>
        delete behavior<thrustReverseSparks>
    }

    on ButtonDown(Click) {
        behavior<laser> {
            loop {
                const anglePerBolt = 0.03rev
                let initialAngle = -(anglePerBolt * (BlastCount - 1)) / 2
                if(BlastCount == 3) {
                    Hear(@assets/basic_laser3.mp3)
                } else {
                    surprise {
                        Hear(@assets/basic_laser.mp3),
                        Hear(@assets/basic_laser2.mp3),
                    }
                }
                for i in Range(0, BlastCount) {
                    Spawn projectile {
                        Projectile(ship=, headingOffset = initialAngle + i*anglePerBolt)
                    }
                }
                await Tick(0.3s)
            }
        }
    }

    on ButtonUp(Click) {
        delete behavior<laser>
    }

    await ReadyPlayer
    
    delete ImageSprite<initialShield>
    
    on BeforeCollide that {
        if QueryCount(filter=Category:Shield) > 0 {
            return
        }
        
        if that.Category.Overlaps(Category:Asteroid | Category:EnemyShip | Category:Explosion) {
            Hear(@assets/explosion1.mp3)
            
            repeat 5 {
                Spark(color=#ffffff, luminous=1, bloom=3, feather=1, dissipate=0.75s, splatter=1, speed=10)
            }
            
            Transmission(audience=Audience:All) {
                P {
                    PlayerNameDisplay
                    "'s ship was destroyed."
                }
            }
            
            Expire
            break
        }
    }
}
