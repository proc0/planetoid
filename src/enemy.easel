pub tangible category Category:Enemy

pub symbol Drone
pub symbol Fighter
pub symbol Leader
pub symbol None


pub fn ship.Enemy(
    type=None, 
    health=1,
    fireRate=2s,
    killScore=50,
    dropRate=0.3,
    [owner]) {
    use body = ship
    use radius = 1
    use speed = 8
    use layer = 5
    
    Health = health
    const maxHealth = health
    const lowHealth = maxHealth/3
    
    Body(pos=RandomSpawnTopPos, velocity=@(0, speed))
    PolygonCollider(shape=Circle(3), category=Category:Enemy, density=1)

    if type == Drone {
        ImageSprite(@assets/drone.svg, color=#ee1111, shading=0.3, shadow=0, scale=4)
    } else if type == Fighter {
        ImageSprite(@assets/drone.svg, color=#ee11ff, shading=0.3, shadow=0, scale=4)
    } else if type == Leader {
        ImageSprite(@assets/ship.svg, color=#eeee11, shading=0.3, shadow=0, scale=4)   
    } else {
        PolygonSprite(shape=Circle, color=#ff0000)        
    }
    
    RecoverSpeed
    TeleportBoundsX
    DespawnBoundsY

    once Tick {
       Heading = HALF_PI 
    }

    on Tick(4s) {
        Velocity = @(0, Speed*Random)
    }

    on Tick(fireRate) {
        let hero = QueryNearest(filter=Category:Ship)
        if hero {
            Spawn projectile {
                Bomb(ship=, target=hero.Pos)
            }
        }
    }

    on BeforeCollide that {
        if that.Category.Overlaps(Category:Weapon | Category:Asteroid) {
            Strobe(shine=0.5, dissipate=0.5s)
            
            Health -= Settings[that.Category.ToString].damage

            if Health <= lowHealth {
                behavior<malfunction> on Tick(0.5s) {
                    repeat 7 { 
                        Spark(radius=Random*2, bodyOffset=RandomVector*radius, color=#222222, shine=0, splatter=1, fade=0.5, dissipate=1s, radius=Random, velocity=-Velocity) 
                    }
                    
                    repeat 4 {
                        Spark(radius=0.5, layer=6, dissipate=0.1s, color=#ffcc00, luminous=1, bloom=3, speed=30, velocity=RandomVector*40, splatter=1)
                    }

                    Velocity = Velocity + RandomVector*PickRandom([-2,2])
                }
            }
            
            if Health <= 0 {
                Hear(@assets/explosion2.mp3)
                
                repeat 10 {
                    Spark(radius=4, color=#fbec8e, luminous=1, bloom=3, feather=1, dissipate=0.75s, splatter=1, speed=10)
                }
                
                if Random > 1-dropRate {
                    Spawn powerup {
                        surprise {                
                            Powerup(pos=Pos, type=EnergyShield),
                            Powerup(pos=Pos, type=MultiLaser),
                            Powerup(pos=Pos, type=HealthPickup),
                        }
                    }
                }

                if that.Category == Category:Weapon {
                    owner.Score += killScore
                }
                
                Expire
                break
            }
        }
    }

    once BeforeDespawn {
        repeat 5 {
            Spark(color=#ffffff, luminous=1, bloom=3, feather=1, dissipate=0.75, splatter=1, speed=10)
        }
    }
}