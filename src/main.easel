pub prop owner.Score = 0
pub prop owner.Time = 0

pub game fn World.Main(maxHumanPlayers=3) {
    SolidBackground(color=#000000)
    Camera(@(0,0), radius=BOUNDS_RADIUS)
    Viewport(aspectRatio=1)

    Spawn starfield {
        Starfield
    }

    Spawn beltoid {
        Beltoid
    }
    
    Spawn bounds {
        Bounds(posY=TOP+2)
        Subspawn bounds {
            Bounds(posY=BOTTOM-2)
        }
    }
    
    SpawnEachPlayer owner {
        Subspawn ship {
            Ship

            once BeforeDespawn {
                Eliminate
            }
        }

        TopContent {
            with Score, Time {
                HStack(width=BOUNDS_WIDTH, hPadding=5) {
                    HStack(width=BOUNDS_WIDTH/2, align=Align:Left) {
                        H1 { %(Time) }
                    }
                    HStack(width=BOUNDS_WIDTH/2, align=Align:Right) {
                        H1 { %(Score) }
                    }
                }
            }
        }

        Level
        BeginLevel(LEVEL_START)
        
        once AfterEliminated {
            const scoreTime = "Time " + Time + " | Score " + Score
            
            OverlayContent {
                VStack(align=Align:Center) {
                    Standout(fontSize=5) { FlyInWords("Destroyed") }
                    Standout(fontSize=2) { FlyInWords(scoreTime) }

                    VStack(animate=Animate:FlyFromBottom, align=Align:Center, vPadding=2) {
                        RaisedButton(Main, fontSize=2, hPadding=2.5, vPadding=1, backgroundColor=Color:Primary) { 
                            "Retry" 
                        }
                    }
                }
            }
        }


        on Tick(TicksPerSecond) {
            Time += 1
        }
    }

    
    ButtonRemap(Tap, Click)
}
