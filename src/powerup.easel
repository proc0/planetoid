pub tangible category Category:Powerup

pub symbol EnergyShield
pub symbol MultiLaser

signal powerup.NearShip

pub fn powerup.Powerup(pos=@(0,0), type) {
    use body=powerup,
    use radius=1.5
    use speed=10
    use luminous=1

    const MAGNET_RADIUS = 12
    const LIFETIME = 7s
    const BG_COLOR = #00ffaa
    
    Body(pos=, velocity=@(Random, speed*Random))
    PolygonCollider(shape=Circle, category=Category:Powerup, collideWith=Category:None, sense=Category:Ship, density=1)

    if type == EnergyShield {    
        PolygonSprite(shape=Circle, color=BG_COLOR, bloom=3, opacity=0.5)
        ImageSprite(@shield.svg, radius=0.85*radius, shadow=0.5)
    } else if type == MultiLaser {
        PolygonSprite(shape=Circle, color=BG_COLOR, bloom=3, shading=0.5, opacity=0.5)
        ImageSprite(@triblast.svg, radius=0.85*radius, shading=0.5, shadow=0.5)
    }
    
    RecoverSpeed
    DecayTurnRate
    DespawnBoundsY
    TeleportBoundsX

    on Tick(0.1s) {
        let entities = QueryWithinRadius(radius=MAGNET_RADIUS, filter=Category:Ship)
        if entities.Length {
            let shipPos = powerup.Pos.Towards(entities[0].Pos, MAGNET_RADIUS)
            if shipPos != MAGNET_RADIUS {
                ApplyImpulse(Direction(shipPos - Pos)*250)
            }
        }
    }
    
    on BeforeCollide that {
        if that.Category.Overlaps(Category:Ship) {
            use ship = that

            if type == EnergyShield {
                if !PreviousShield {                
                    PreviousShield = ship.Subspawn shield {
                        Shield(ship=, energy=ship.Health >= POWERUP_INIT_SHIELD_ENERGY ? POWERUP_INIT_SHIELD_ENERGY : ship.Health)
                    }
                } else {
                    ship.Energy += POWERUP_SHIELD_ENERGY
                }
            } else if type == MultiLaser {
                ship.Subspawn {
                    once Tick(10s) { Expire }
                    give BlastCount(3)
                }
            }

            Expire
            break
        }
    }

    once Tick(LIFETIME-3s) {
        PolygonSprite(shape=Circle, color=BG_COLOR, bloom=3, flicker=1, opacity=0.5, shadow=0.5)
    }
    
    once Tick(LIFETIME) { Expire }
}