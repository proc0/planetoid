prop asteroid.Health

pub fn asteroid.Asteroid(radius=4, pos=RandomSpawnTopPos(4), speed=30*Random, health=10, dissipate=0.75s, color=#444444, generation=0, parent?) {
    use body = asteroid
    use layer=5
    Health = health
    
    Body(pos=, velocity=@(speed * Random * PickRandom([0.2,-0.2]), speed))
    PolygonCollider(shape=Circle, category=Category:Asteroid)
    ImageSprite(@assets/asteroid.png, shading=0.25, shadow=0)
    
    RecoverSpeed
    DespawnBounds

    on BeforeCollide that {
        repeat 5 { Spark(color=#222222, shine=0, splatter=1, dissipate=0.5s, radius=Random, velocity=Velocity*RandomVector) }
        
        const MaxGenerations = 2
        if Health <= 0 {
            Expire
            if generation < MaxGenerations {
                let parent = asteroid
                let nextGen = generation + 1
                repeat 2 {
                    Spawn asteroid {
                        Asteroid(radius=0.6*radius, pos=Pos+RandomVector, speed=1.3*speed, health=8/nextGen, 
                                 dissipate=0.7*dissipate, color=Mix(0.25, color, #ffff00), generation=nextGen, parent=)
                    }
                }
            }
        }
        
        // TODO add damage prop on ship, projectile and use it here
        if that.Category.Overlaps(Category:Asteroid) {
            Health -= 8
            // Velocity += that.Velocity 
        } else {
            Health -= 3
        }
    }

}