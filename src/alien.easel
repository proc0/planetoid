prop ship.Health

pub fn ship.Alien {
    use body=ship
    use radius=1, speed=8
    use maxHealth=5

    Health = maxHealth
    Body(
        pos=@(Random * (BoundaryRadius - 10) * PickRandom([1,-1]), -BoundaryRadius - radius),
        velocity=@(0, speed),
    )

    ImageSprite(@assets/alien_ship_body.png, shading=0.25, shadow=0, scale=5)
    PolygonCollider(shape=Circle(3), category=Category:Alien, density=1)
    RecoverSpeed
    WrapOutsideBoundary
    DespawnOutsideVerticalBoundary

    // with Health {
    //     let proportion = Health / maxHealth
    //     let color = proportion.BlendHue(#ff6600, #66ff00)
    //     ImageSprite(@assets/alien_ship_body.png, color=, shading=0.25, shadow=0.5, scale=3.5)
    // }
    once Tick {
       Heading = RIGHT_ANGLE 
    }
    once BeforeDespawn {
        repeat 5 {
            Spark(color=#ffffff, luminous=1, bloom=3, feather=1, dissipate=0.75, splatter=1, speed=10)
        }
    }

    on BeforeCollide that {
        if that.Category.Overlaps(Category:Projectile | Category:Asteroid) {
            if Health <= 2 {
                ImageSprite(@assets/alien_ship_body.png, color=#ff0000, luminous=0.4, overlay=10, shine=0.2, opacity=0.7, shading=0, shadow=0, scale=5)
            }
            Strobe(shine=0.5, dissipate=0.5s)
            
            Health -= 1
            if Health <= 0 {
                Hear(@assets/explosion2.mp3)
                
                repeat 10 {
                    Spark(radius=4, color=#fbec8e, luminous=1, bloom=3, feather=1, dissipate=0.75s, splatter=1, speed=10)
                }
                const MaxPowerups = 1
                const probability = Random > 0.7
                if QueryCount(filter=Category:Powerup) < MaxPowerups && probability {
                    Spawn powerup {
                        surprise {                
                            ShieldPowerup(pos=Pos),
                            TriblastPowerup(pos=Pos),
                        }
                    }
                }
                Expire
                break
            }
        }

        if that.Category.Overlaps(Category:Asteroid) {
            Health -= 3
            if Health <= 0 {
                Expire
                break
            }
        }
    }

    on Tick(4s) {
        Velocity = @(0, Speed*Random)
    }

    on Tick(2s) {
        let hero = QueryNearest(filter=Category:Ship)
        if hero {
            Spawn projectile {
                EnergonBoom(ship=, target=hero.Pos)
            }
        }
    }
    
}